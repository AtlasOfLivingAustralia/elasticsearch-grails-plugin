// Publish gh-pages on github
apply plugin: "org.ajoberstar.github-pages"

ext {
    githubApiKey = System.getProperty('githubApiKey')
}

githubPages {
    repoUri = 'https://github.com/puneetbehl/elasticsearch-grails-plugin.git'

    credentials {
        username = project.hasProperty('githubApiKey') ? project.githubApiKey : ''
        password = ''
    }

    pages {
        from "${buildDir}/docs"
    }
}

task publishDocs(dependsOn: [docs, publishGhPages])

apply plugin:"maven-publish"
publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {

            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
            artifact bootJar
        }
    }
}
